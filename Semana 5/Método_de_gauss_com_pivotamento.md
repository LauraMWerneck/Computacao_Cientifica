## CÃ³digo
```c
/*Use a eliminaÃ§Ã£o de Gauss com pivotamento para resolver o sistema:
3 ð‘¥1 + ð‘¥2 + 2 ð‘¥3 = 2
6 ð‘¥1 + 3 ð‘¥2 âˆ’ ð‘¥3 = âˆ’2
2 ð‘¥1 + 3 ð‘¥2 + ð‘¥3 = 1
*/

function x=gaussp(A, b)
    // EliminaÃ§Ã£o de Gauss com pivotamento
    // onde x vetor soluÃ§Ã£o
    // A Ã© a matriz de coeficientes
    // b Ã© o vetor de termos constantes
    // exec('path\gaussp.sci',-1)
    // x=gausp(A, b)
    [m,n] = size(A);
    if m~=n then
        error('A deve ser uma matriz quadrada.');
    end
    n=length(b)
    if m~=n then
        error('DimensÃ£o incorreta de b');
    end
    nb = n + 1;
    A = [A b]; // pbtendo a matriz aumentada
    // eliminaÃ§Ã£o progressiva
    for i=1:n-1
        // pivotamento parcial
        // encontrar o maior coef. da coluna abaixo do pivo
        [maior,k]=max(abs(A(i:n,i)));
        l = i + k - 1;
        if l~=i then
            A([l,i],:)=A([i,l],:);
        end
        for j=i+1:n
            A(j,i) = A(j,i)/A(i,i);
            A(j,i+1:nb)=A(j,i+1:nb)-A(j,i)*A(i,i+1:nb);
            disp(A);
        end
    end
    // substituiÃ§Ã£o regressiva
    x = zeros(n,1);
    x(n)= A(n,nb)/A(n,n);
    for i=n-1:-1:1
        x(i) = (A(i,nb) - A(i,(i+1):n) * x((i+1):n)) /A(i,i);
    end
endfunction


/*     Console
--> A = [3 1 2; 6 3 -1; 2 3 1]
 A  = 

   3.   1.   2.
   6.   3.  -1.
   2.   3.   1.

--> b = [2;-2;1]
 b  = 

   2.
  -2.
   1.

--> x=gaussp(A, b)

   6.    3.   -1.   -2.
   0.5  -0.5   2.5   3.
   2.    3.    1.    1.

         column 1 to 3
   6.          3.   -1.       
   0.5        -0.5   2.5      
   0.3333333   2.    1.3333333
         column 4
  -2.       
   3.       
   1.6666667

         column 1 to 3
   6.          3.    -1.       
   0.3333333   2.     1.3333333
   0.5        -0.25   2.8333333
         column 4
  -2.       
   1.6666667
   3.4166667
 x  = 

  -0.1470588
   0.0294118
   1.2058824
*/
```
